# General profile for bike trip planning

---context:global   # following code refers to global config

# Bike profile
assign validForBikes = true

# Use the following switches to change behaviour
assign   allow_steps              = true   # %allow_steps% | Povolit trasu přes schody. | boolean
assign   allow_ferries            = false   # %allow_ferries% | Povolit trasu přes přívozy. | boolean
assign   ignore_cycleroutes       = false  # %ignore_cycleroutes% | Nezahrnovat cyklotrasy do výpočtu. | boolean
assign   stick_to_cycleroutes     = false  # %stick_to_cycleroutes% | Držet se převážně cyklotras. | boolean
assign   stick_to_hikeroutes     = false  # %stick_to_hikeroutes% | Držet se převážně turistických značek. | boolean
assign   aphalt_avoideness     = 0  # %aphalt_avoideness% | Čím větší hodnota, tím více se vyhýbá asfaltu. | number
assign   primary_avoideness     = 0  # %primary_avoideness% | Čím větší hodnota, tím více se vyhýbá silnicím I. třídy. | number
assign   secondary_avoideness     = 0  # %secondary_avoideness% | Čím větší hodnota, tím více se vyhýbá silnicím II. třídy. | number
assign   traffic_lights_avoideness     = 300  # %traffic_lights_avoideness% | Čím větší hodnota, tím více se vyhýbá semaforům | number

assign   avoid_unsafe             = false  # %avoid_unsafe% | Vyhýbat se větším silnicím | boolean
assign   add_beeline              = false
assign   consider_noise           = false  # %consider_noise% | Vyhnout se trasám na nebo při velkých silnicích. | boolean
assign   consider_river           = false  # %consider_river% | Aktivujte pro preferování trasy podél řek, jezer apod. | boolean
assign   consider_forest          = false  # %consider_forest% | Aktivujte pro preferování trasy v lesích nebo parcích | boolean
assign   consider_town            = false  # %consider_town% | Aktivujte pro co největší obcházení měst/velkých sídel | boolean
assign   consider_traffic         = false  # %consider_traffic% | Aktivujte pro zohlednění odhadů provozu | boolean



# Change elevation parameters
assign   consider_elevation       = true   # %consider_elevation% | Brát v úvahu převýšení | boolean

assign downhillcost       = 100    # %downhillcost% | Náklad pro sjíždění z kopce | number
assign downhillcutoff     = 10   # %downhillcutoff% | Sklony pod touto hodnotou v procentech se nepočítají. | number
assign uphillcost         = 100     # %uphillcost% | Náklad pro stoupání do kopce | number
assign uphillcutoff       = 10   # %uphillcutoff% | Sklony pod touto hodnotou v procentech se nepočítají.  | number

assign downhillcost       = if consider_elevation then downhillcost else 0
assign uphillcost         = if consider_elevation then uphillcost else 0

# Kinematic model parameters (travel time computation)
assign totalMass  = 90     # %totalMass% | Hmotnost (v kg) kola + jezdce pro výpočet času jízdy | number
assign maxSpeed   = 60     # %maxSpeed% | Absolutní maximální rychlost (v km/h), pro výpočet času jízdy | number
assign S_C_x      = 0.225  # %S_C_x% | Součin koeficientu odporu vzduchu a referenční plochy (v m^2), pro výpočet času jízdy | number
assign C_r        = 0.01   # %C_r% | Koeficient valivého odporu (bezrozměrný), pro výpočet času jízdy | number
assign bikerPower = 100    # %bikerPower% | Průměrný výkon (ve W) dodávaný jezdcem pro výpočet času jízdy | number

# Turn instructions settings
assign turnInstructionMode          = 1
assign turnInstructionCatchingRange = 40
assign turnInstructionRoundabouts   = true
assign considerTurnRestrictions     = true

assign processUnusedTags            = false

---context:way   # following code refers to way-tags

# classifier constants
assign classifier_none  = 1
assign classifier_ferry = 2

#
# pre-calculate some logical expressions
#

assign any_hiking_route or route=hiking         or route_hiking_iwn=yes
						or route_hiking_nwn=yes	or route_hiking_rwn=yes
						or route_hiking_lwn=yes or route_hiking_=yes
                        or route_foot_=yes      or route_foot_nwn=yes
						or route_foot_rwn=yes      route_foot_lwn=yes

assign any_cycleroute =
     if      route_bicycle_icn=yes then true
     else if route_bicycle_ncn=yes then true
     else if route_bicycle_rcn=yes then true
     else if route_bicycle_lcn=yes then true
     else false

assign nodeaccessgranted =
     if any_cycleroute then true
     else lcn=yes

assign is_ldcr =
     if ignore_cycleroutes then false
     else any_cycleroute

assign is_hike =
     if stick_to_hikeroutes then any_hiking_route
     else false

assign isbike = or bicycle_road=yes or bicycle=yes or or bicycle=permissive bicycle=designated lcn=yes
assign ispaved = surface=paved|asphalt|concrete|paving_stones|sett
assign isunpaved = not or surface= or ispaved surface=fine_gravel|cobblestone
assign probablyGood = or ispaved and ( or isbike highway=footway ) not isunpaved


#
# this is the cost (in Meter) for a 90-degree turn
# The actual cost is calculated as turncost*cos(angle)
# (Suppressing turncost while following longdistance-cycleways
# makes them a little bit more magnetic)
#
assign turncost = if is_ldcr then 0
                  else if junction=roundabout then 0
                  else 90


#
# for any change in initialclassifier, initialcost is added once
#
assign initialclassifier =
     if route=ferry then classifier_ferry
     else classifier_none


#
# calculate the initial cost
# this is added to the total cost each time the costfactor
# changed
#
assign initialcost =
     if ( equal initialclassifier classifier_ferry ) then 10000
     else 0

#
# implicit access here just from the motorroad tag
# (implicit access rules from highway tag handled elsewhere)
#
assign defaultaccess =
       if access= then not motorroad=yes
       else if access=private|no then false
       else true

#
# calculate logical bike access
#
assign bikeaccess =
       switch bicycle=
              switch bicycle_road=yes
                 true
                 switch vehicle=
                        ( if highway=footway then false else defaultaccess )
                        not vehicle=private|no
              not or bicycle=private or bicycle=no bicycle=dismount

assign accesspenalty
       switch bikeaccess
              0
              switch any_cycleroute
                  15
                  10000

#
# handle one-ways. On primary roads, wrong-oneways should
# be close to forbidden, while on other ways we just add
# 4 to the costfactor (making it at least 5 - you are allowed
# to push your bike)
#
assign badoneway =
       if reversedirection=yes then
         if oneway:bicycle=yes then true
         else if oneway= then junction=roundabout
         else oneway=yes|true|1
       else oneway=-1

assign onewaypenalty =
       if ( badoneway ) then
       (
         if      ( cycleway=opposite|opposite_lane|opposite_track       ) then 0
         else if ( cycleway:left=opposite|opposite_lane|opposite_track  ) then 0
         else if ( cycleway:right=opposite|opposite_lane|opposite_track ) then 0
         else if ( oneway:bicycle=no                         ) then 0
         else if ( cycleway:left:oneway=no                   ) then 0
         else if ( cycleway:right:oneway=no                  ) then 0
         else if ( junction=roundabout|circular              ) then 60
         else if ( highway=primary|primary_link              ) then 50
         else if ( highway=secondary|secondary_link          ) then 30
         else if ( highway=tertiary|tertiary_link            ) then 20
         else 4.0
       )
       else 0.0

# add estimate tags
assign traffic_penalty
   switch consider_traffic
      switch estimated_traffic_class=       0
      switch estimated_traffic_class=1|2    0.2
      switch estimated_traffic_class=3      0.4
      switch estimated_traffic_class=4      0.6
      switch estimated_traffic_class=5      0.8
      switch estimated_traffic_class=6|7    1 99 0


assign noise_penalty
   switch consider_noise
     switch estimated_noise_class=  0
     switch estimated_noise_class=1  0.3
     switch estimated_noise_class=2  0.5
     switch estimated_noise_class=3  0.8
     switch estimated_noise_class=4  1.4
     switch estimated_noise_class=5  1.7
     switch estimated_noise_class=6  2 0 0

assign no_river_penalty
   switch consider_river
     switch estimated_river_class=  2
     switch estimated_river_class=1  1.3
     switch estimated_river_class=2  1
     switch estimated_river_class=3  0.7
     switch estimated_river_class=4  0.4
     switch estimated_river_class=5  0.1
     switch estimated_river_class=6  0 99 0

assign no_forest_penalty
   switch consider_forest
     switch estimated_forest_class=  1
     switch estimated_forest_class=1  0.5
     switch estimated_forest_class=2  0.4
     switch estimated_forest_class=3  0.25
     switch estimated_forest_class=4  0.15
     switch estimated_forest_class=5  0.1
     switch estimated_forest_class=6  0 99 0

assign town_penalty
   switch consider_town
     switch estimated_town_class=  0
     switch estimated_town_class=1  0.5
     switch estimated_town_class=2  0.9
     switch estimated_town_class=3  1.2
     switch estimated_town_class=4  1.3
     switch estimated_town_class=5  1.4
     switch estimated_town_class=6  1.6 99 0

assign smoothFactor
  switch smoothness=excellent 0.8
  switch smoothness=good      0.9
  switch smoothness=intermediate 1
  switch smoothness=bad       1.25
  switch smoothness=very_bad  1.5
  switch smoothness=horrible  2.0
  switch smoothness=very_horrible 3.0
  switch smoothness=impassable 100.0
  1.0

#
# calculate the cost-factor, which is the factor
# by which the distance of a way-segment is multiplied
# to calculate the cost of that segment. The costfactor
# must be >=1 and it's supposed to be close to 1 for
# the type of way the routing profile is searching for
#
assign isresidentialorliving = or highway=residential|living_street living_street=yes
assign costfactor

  #
  # exclude rivers, rails etc.
  #
  if ( and highway= not route=ferry ) then 10000

  #
  # exclude motorways and proposed, abandoned under construction roads
  #
  else if ( highway=motorway|motorway_link          ) then   10000
  else if ( highway=proposed|abandoned|construction ) then   10000

  #
  # all other exclusions below (access, steps, ferries,..)
  # should not be deleted by the decoder, to be available
  # in voice-hint-processing
  #
  else min 9999

  add town_penalty
  add no_forest_penalty
  add no_river_penalty
  add noise_penalty
  add traffic_penalty

  multiply smoothFactor

  #
  # apply oneway-and access-penalties
  #
  add max onewaypenalty accesspenalty

  add if ( or surface=asphalt highway=primary|primary_link|secondary|secondary_link|tertiary|tertiary_link ) then ( if greater aphalt_avoideness 0 then aphalt_avoideness else 0 ) else 0
  add if ( highway=primary|primary_link ) then ( if greater primary_avoideness 0 then primary_avoideness else 0 ) else 0
  add if ( highway=secondary|secondary_link ) then ( if greater secondary_avoideness 0 then secondary_avoideness else 0 ) else 0

  #
  # steps and ferries are special. Note this is handled
  # before the cycleroute-switch, to be able
  # to really exclude them be setting cost to infinity
  #
  if ( highway=steps ) then ( if allow_steps then 40 else 10000 )
  else if ( route=ferry   ) then ( if allow_ferries then 5.67 else 10000 )
  #
  # handle long-distance cycle-routes.
  #
  else if ( is_ldcr ) then 1                   # always treated as perfect (=1)
  else if ( is_hike ) then 0.9
  else
  add ( if stick_to_cycleroutes then 0.5 else 0.05 )  # everything else somewhat up

  #
  # some other highway types
  #
  if      ( highway=pedestrian                ) then 3
  else if ( highway=bridleway                 ) then 5
  else if ( highway=cycleway                  ) then 1
  else if ( isresidentialorliving             ) then ( if isunpaved then 1.5 else 1.1 )
  else if ( highway=service                   ) then ( if isunpaved then 1.6 else 1.3 )

  #
  # tracks and track-like ways are rated mainly be tracktype/grade
  # But note that if no tracktype is given (mainly for road/path/footway)
  # it can be o.k. if there's any other hint for quality
  #
  else if ( highway=track|road|path|footway ) then
  (
    if      ( tracktype=grade1 ) then ( if probablyGood then 1.0 else 1.3 )
    else if ( tracktype=grade2 ) then ( if probablyGood then 1.1 else 2.0 )
    else if ( tracktype=grade3 ) then ( if probablyGood then 1.5 else 3.0 )
    else if ( tracktype=grade4 ) then ( if probablyGood then 2.0 else 5.0 )
    else if ( tracktype=grade5 ) then ( if probablyGood then 3.0 else 5.0 )
    else                              ( if probablyGood then 1.0 else 5.0 )
  )

  #
  # When avoiding unsafe ways, avoid highways without a bike hint
  #
  else add ( if ( and avoid_unsafe not isbike ) then 2 else 0 )

  #
  # actuals roads are o.k. if we have a bike hint
  #
       if ( highway=trunk|trunk_link         ) then ( if isbike then 1.5 else 10  )
  else if ( highway=primary|primary_link     ) then ( if isbike then 1.2 else  3  )
  else if ( highway=secondary|secondary_link ) then ( if isbike then 1.1 else 1.6 )
  else if ( highway=tertiary|tertiary_link   ) then ( if isbike then 1.0 else 1.4 )
  else if ( highway=unclassified             ) then ( if isbike then 1.0 else 1.3 )

  #
  # default for any other highway type not handled above
  #
  else 2.0


---context:node  # following code refers to node tags

assign defaultaccess =
       if ( access= ) then true # add default barrier restrictions here!
       else if ( access=private|no ) then false
       else true

assign bikeaccess =
       if nodeaccessgranted=yes then true
       else if bicycle= then
       (
         if vehicle= then defaultaccess
         else not vehicle=private|no
       )
       else not bicycle=private|no|dismount

assign footaccess =
       if bicycle=dismount then true
       else if foot= then defaultaccess
       else not foot=private|no

assign barrierpenalty
    switch barrier=                0
    switch barrier=block|bollard   25
    switch barrier=gate|swing_gate 50
    switch barrier=cycle_barrier   87
                                   139

assign crossingpenalty
    switch highway=traffic_signals traffic_lights_avoideness
    switch highway=crossing 100
    switch railway=level_crossing 300
    switch and highway=give_way direction=forward 100
    0

assign initialcost =
       add barrierpenalty
       add crossingpenalty
       if bikeaccess then 0
       else ( if footaccess then 100 else 1000000 )
