#
# Originaly a fastbike profile
# Modified for road racing
#

---context:global   # following code refers to global config

# Bike profile
assign validForBikes = true

# Use the following switches to change behaviour
assign allow_steps           = true   # %allow_steps% | Povolit schody | boolean

# Change elevation parameters
assign downhillcost       = 0    # %downhillcost% | Cena za jízdu z kopce | number
assign downhillcutoff     = 8   # %downhillcutoff% | Nižší sklon než tento se do jízdy z kopce nezapočítá. | number
assign uphillcost         = 10     # %uphillcost% | Cena za jízdu do kopce | number
assign uphillcutoff       = 0   # %uphillcutoff% | Nižší sklon než tento se do jízdy do kopce nezapočítá.  | number

assign unpavedFactor       = 1    # %unpavedFactor% | Ovlivní routování přes nezpevněné cesty, 0.5 znamená o 50% nižší náklady oproti defaultu | number

# Kinematic model parameters (travel time computation)
assign totalMass  = 86     # %totalMass% | Celková váha (kolo, výbava + jezdec), pouze pro výpočet času jízdy | number
assign maxSpeed   = 60
assign S_C_x      = 0.225  # %S_C_x% | Odpor vzduchu, pouze pro výpočet času jízdy | number
assign C_r        = 0.02   # %C_r% | Odpor plášťů, pouze pro výpočet času jízdy | number
assign bikerPower = 180    # %bikerPower% | Průměrný výkon ve W, pouze pro výpočet času jízdy | number

# Turn instructions settings
assign turnInstructionMode          = 1
assign turnInstructionCatchingRange = 40
assign turnInstructionRoundabouts   = true
assign considerTurnRestrictions     = true

assign processUnusedTags            = true

---context:way   # following code refers to way-tags

#assign any_cycleroute or route_bicycle_icn=yes or route_bicycle_ncn=yes or route_bicycle_rcn=yes route_bicycle_lcn=yes
#assign nodeaccessgranted or any_cycleroute lcn=yes

assign ispaved or surface=paved or surface=asphalt or surface=concrete or surface=paving_stones or surface=sett surface=cobblestone
assign isunpaved not or surface= or ispaved surface=fine_gravel

assign turncost = if junction=roundabout then 0 else 90

# classifier constants
assign classifier_none        = 1
assign classifier_ferry       = 2

assign initialclassifier =
    if route=ferry then classifier_ferry
    else classifier_none

assign initialcost =
    if equal initialclassifier classifier_ferry then 10000
    else 0

assign accesspenalty
    switch motorroad=yes 10000 # I can't access motorroads
    switch highway=motorway|motorway_link 10000 # I can't access highways
    switch or access=private or access=no bicycle=private 10000 # I can't access private roads
    switch or bicycle=no ( and highway=footway and footway=sidewalk not bicycle=yes ) 5 # I can't ride on sidewalks
    0

assign badoneway =
       if reversedirection=yes then
         if oneway:bicycle=yes then true
         else if oneway= then junction=roundabout
         else oneway=yes|true|1
       else oneway=-1

assign onewaypenalty =
       if ( badoneway ) then
       (
         if      ( cycleway=opposite|opposite_lane|opposite_track       ) then 0
         else if ( cycleway:left=opposite|opposite_lane|opposite_track  ) then 0
         else if ( cycleway:right=opposite|opposite_lane|opposite_track ) then 0
         else if ( oneway:bicycle=no                         ) then 0
         else if ( cycleway:left:oneway=no                   ) then 0
         else if ( cycleway:right:oneway=no                  ) then 0
         else if ( junction=roundabout|circular              ) then 60
         else if ( highway=primary|primary_link|secondary|secondary_link              ) then 50
         else if ( highway=tertiary|tertiary_link            ) then 20
         else 6.0
       )
       else 0.0

assign town_penalty
   switch estimated_town_class=  0
   switch estimated_town_class=1  0.2
   switch estimated_town_class=2  0.6
   switch estimated_town_class=3  0.8
   switch estimated_town_class=4  1
   switch estimated_town_class=5  1.2
   switch estimated_town_class=6  2 99

assign smoothFactor
  switch smoothness=excellent 0.8
  switch smoothness=good      0.9
  switch smoothness=intermediate 1
  switch smoothness=bad       1.4
  switch smoothness=very_bad  1.7
  switch smoothness=horrible  2.0
  switch smoothness=very_horrible 3.0
  switch smoothness=impassable 100.0
  switch surface=fine_gravel 1.3
  switch surface=cobblestone 1.4
  switch highway=primary|primary_link|secondary|secondary_link 0.95 # You can expect better quality surface
  1.0

assign baseCost 1.25

assign isreversed = reversedirection=yes

assign costfactor
  add onewaypenalty
  add accesspenalty
  add town_penalty

  multiply smoothFactor

  switch    route=ferry            10000
  switch    highway=motorway|motorway_link|trunk|trunk_link|construction|proposed|abandoned            10000
  switch    highway=primary|primary_link|secondary|secondary_link|tertiary|tertiary_link|cycleway      baseCost
  switch    highway=unclassified|pedestrian|bridleway|rest_area                                        switch isunpaved multiply baseCost 2 baseCost
  switch    highway=steps                                                                              switch allow_steps 10 10000
  switch    highway=residential|living_street                                                          switch isunpaved multiply multiply baseCost 2 unpavedFactor multiply baseCost 1.1
  switch    highway=service                                                                   switch isunpaved multiply multiply baseCost 2 unpavedFactor baseCost
  switch or highway=track or highway=road or highway=path highway=footway
   switch tracktype=grade1                                                                             switch isunpaved multiply multiply baseCost 3 unpavedFactor baseCost
   switch tracktype=grade2                                                                             switch isunpaved multiply multiply baseCost 5 unpavedFactor multiply baseCost 1.7
   switch tracktype=grade3                                                                             multiply multiply baseCost 7 unpavedFactor
   switch tracktype=grade4                                                                             multiply multiply baseCost 10 unpavedFactor
   switch tracktype=grade5                                                                             multiply multiply baseCost 10 unpavedFactor
   switch or bicycle=designated bicycle_road=yes                                                       baseCost
   switch ispaved                                                                                      baseCost multiply multiply baseCost 10 unpavedFactor
  10000 # I can't ride on rails

---context:node  # following code refers to node tags

assign applyDirection =
    switch and direction=backward way:isreversed 1
    switch and direction=backward not way:isreversed 0
    switch and direction=forward not way:isreversed 1
    switch and direction=forward way:isreversed 0
    0

assign applyTrafficDirection
    switch and traffic_signals:direction=backward way:isreversed 1
    switch and traffic_signals:direction=backward not way:isreversed 0
    switch and traffic_signals:direction=forward not way:isreversed 1
    switch and traffic_signals:direction=forward way:isreversed 0
    0

assign barrierPenalty
	switch barrier=block|bollard 25
	switch barrier=gate|swing_gate|lift_gate|entrance 50
	switch barrier=cycle_barrier 90
	switch and traffic_calming=bump applyDirection 25
	switch and traffic_calming=cushion applyDirection 10
		0

assign crossingsPenalty
	switch railway=level_crossing 300
	switch and highway=traffic_signals applyTrafficDirection 200
	switch highway=crossing 25
	switch and highway=give_way applyDirection 80
	switch and highway=stop applyDirection 120
		0

assign initialcost =
	add barrierPenalty
	add crossingsPenalty
    0
