#
# Originaly a fastbike profile
# Modified for road racing
#

---context:global   # following code refers to global config

# Bike profile
assign validForBikes = true

# Use the following switches to change behaviour
assign allow_steps           = true   # %allow_steps% | Povolit schody | boolean

assign consider_town         = true

# Change elevation parameters
assign consider_elevation = true
assign downhillcost       = 0    # %downhillcost% | Cena za jízdu z kopce | number
assign downhillcutoff     = 8   # %downhillcutoff% | Nižší sklon než tento se do jízdy z kopce nezapočítá. | number
assign uphillcost         = 10     # %uphillcost% | Cena za jízdu do kopce | number
assign uphillcutoff       = 0   # %uphillcutoff% | Nižší sklon než tento se do jízdy do kopce nezapočítá.  | number

assign unpavedFactor       = 1    # %unpavedFactor% | Ovlivní routování přes nezpevněné cesty, 0.5 znamená o 50% nižší náklady oproti defaultu | number

assign downhillcost       = if consider_elevation then downhillcost else 0
assign uphillcost         = if consider_elevation then uphillcost else 0

# Kinematic model parameters (travel time computation)
assign totalMass  = 86     # %totalMass% | Celková váha (kolo, výbava + jezdec), pouze pro výpočet času jízdy | number
assign maxSpeed   = 60
assign S_C_x      = 0.225  # %S_C_x% | Odpor vzduchu, pouze pro výpočet času jízdy | number
assign C_r        = 0.02   # %C_r% | Odpor plášťů, pouze pro výpočet času jízdy | number
assign bikerPower = 180    # %bikerPower% | Průměrný výkon ve W, pouze pro výpočet času jízdy | number

# Turn instructions settings
assign turnInstructionMode          = 1
assign turnInstructionCatchingRange = 40
assign turnInstructionRoundabouts   = true
assign considerTurnRestrictions     = true

assign processUnusedTags            = false

---context:way   # following code refers to way-tags

assign any_cycleroute or route_bicycle_icn=yes or route_bicycle_ncn=yes or route_bicycle_rcn=yes route_bicycle_lcn=yes
assign nodeaccessgranted or any_cycleroute lcn=yes

assign ispaved or surface=paved or surface=asphalt or surface=concrete or surface=paving_stones surface=sett
assign isunpaved not or surface= or ispaved or surface=fine_gravel surface=cobblestone

assign turncost = if junction=roundabout then 0
                  else 90

assign initialclassifier =
     if route=ferry then 1
     else 0

assign initialcost switch route=ferry 10000 0

#
# implicit access here just from the motorroad tag
# (implicit access rules from highway tag handled elsewhere)
#
assign defaultaccess
       switch access=
              (
                    if motorroad=yes then false
                    else if highway=motorway|motorway_link then false
                    else true
              )
              switch or access=private access=no
                     false
                     true

#
# calculate logical bike access
#
assign bikeaccess =
       switch bicycle=
              switch bicycle_road=yes
                 true
                 switch vehicle=
                        ( if and highway=footway footway=sidewalk then false else defaultaccess )
                        not vehicle=private|no
              not or bicycle=private or bicycle=no bicycle=dismount

assign accesspenalty
       switch bikeaccess
              0
              switch any_cycleroute
                  15
                  10000


#
# handle one-ways. On primary roads, wrong-oneways should
# be close to forbidden, while on other ways we just add
# 6 to the costfactor (making it at least 7 - you are allowed
# to push your bike)
#
assign badoneway =
       if reversedirection=yes then
         if oneway:bicycle=yes then true
         else if oneway= then junction=roundabout
         else oneway=yes|true|1
       else oneway=-1

assign onewaypenalty =
       if ( badoneway ) then
       (
         if      ( cycleway=opposite|opposite_lane|opposite_track       ) then 0
         else if ( cycleway:left=opposite|opposite_lane|opposite_track  ) then 0
         else if ( cycleway:right=opposite|opposite_lane|opposite_track ) then 0
         else if ( oneway:bicycle=no                         ) then 0
         else if ( cycleway:left:oneway=no                   ) then 0
         else if ( cycleway:right:oneway=no                  ) then 0
         else if ( junction=roundabout|circular              ) then 60
         else if ( highway=primary|primary_link              ) then 50
         else if ( highway=secondary|secondary_link          ) then 30
         else if ( highway=tertiary|tertiary_link            ) then 20
         else 6.0
       )
       else 0.0

assign town_penalty
   switch consider_town
     switch estimated_town_class=  0
     switch estimated_town_class=1  0.2
     switch estimated_town_class=2  0.6
     switch estimated_town_class=3  0.8
     switch estimated_town_class=4  1
     switch estimated_town_class=5  1.2
     switch estimated_town_class=6  2 99 0

assign smoothFactor
  switch smoothness=excellent 0.8
  switch smoothness=good      0.9
  switch smoothness=intermediate 1
  switch smoothness=bad       1.4
  switch smoothness=very_bad  1.7
  switch smoothness=horrible  2.0
  switch smoothness=very_horrible 3.0
  switch smoothness=impassable 100.0
  switch surface=fine_gravel 1.3
  1.0

assign baseCost 1.25

assign costfactor
  add max onewaypenalty accesspenalty
  add town_penalty

  multiply smoothFactor

  switch    highway=motorway|motorway_link|trunk|trunk_link|construction|proposed|abandoned            10000
  switch    highway=primary|primary_link|secondary|secondary_link|tertiary|tertiary_link|cycleway      baseCost
  switch    highway=unclassified|pedestrian|bridleway|rest_area                                        switch isunpaved multiply baseCost 2 baseCost
  switch    highway=steps                                                                              switch allow_steps 10 10000
  switch    highway=residential|living_street                                                          switch isunpaved multiply multiply baseCost 2 unpavedFactor multiply baseCost 1.1
  switch    highway=service|cycleway                                                                   switch isunpaved multiply multiply baseCost 2 unpavedFactor baseCost
  switch or highway=track or highway=road or highway=path highway=footway
   switch tracktype=grade1                                                                             switch isunpaved multiply multiply baseCost 3 unpavedFactor baseCost
   switch tracktype=grade2                                                                             switch isunpaved multiply multiply baseCost 5 unpavedFactor multiply baseCost 1.7
   switch tracktype=grade3                                                                             multiply multiply baseCost 7 unpavedFactor
   switch tracktype=grade4                                                                             multiply multiply baseCost 10 unpavedFactor
   switch tracktype=grade5                                                                             multiply multiply baseCost 10 unpavedFactor
   switch or bicycle=designated bicycle_road=yes                                                       baseCost
   switch ispaved                                                                                      baseCost multiply multiply baseCost 10 unpavedFactor
  0

---context:node  # following code refers to node tags

assign defaultaccess =
       if ( access= ) then true # add default barrier restrictions here!
       else if ( access=private|no ) then false
       else true

assign bikeaccess =
       if nodeaccessgranted=yes then true
       else if bicycle= then
       (
         if vehicle= then defaultaccess
         else not vehicle=private|no
       )
       else not bicycle=private|no|dismount

assign footaccess =
       if bicycle=dismount then true
       else if foot= then defaultaccess
       else not foot=private|no

assign barrierPenalty
	switch barrier=block|bollard 25
	switch barrier=gate|swing_gate|lift_gate|entrance 50
	switch barrier=cycle_barrier 90
	switch and traffic_calming=bump not direction=backward 25
	switch and traffic_calming=cushion not direction=backward 10
		0

assign crossingsPenalty
	switch railway=level_crossing 300
	switch and highway=traffic_signals not traffic_signals:direction=backward 200
	switch highway=crossing 25
	switch and highway=give_way not direction=backward 80
	switch and highway=stop not direction=backward 120
		0

assign initialcost =
	add barrierPenalty
	add crossingsPenalty
    if bikeaccess then 0
    else ( if footaccess then 100 else 1000000 )
